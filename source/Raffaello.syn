module Rafaello {
    
    // ***** BarChart ********************

    method string BarChart(int32 width, int32 height, json chartData) {
        string chart
        json attr = ParseJSON("{}")
        int32 points
        int32 i 
        int32 max
        int32 scaleMargin
        int32 maxScaleLen
        int32 barsOffset
        int32 scaleOffset
        int32 yAxisOffset
        json dataSet
        string position = "left"

        dataSet = chartData["datasets"][0]

        if chartData.HasKey("options") {            
            if chartData["options"].HasKey("yaxis") {                
                if chartData["options"]["yaxis"].HasKey("position") {
                    position = Lowercase(chartData["options"]["yaxis"]["position"])
                }
            }
        }

        chart = "<svg "
        chart = chart + " width=\"" + width + "\""
        chart = chart + " height=\"" + height + "\""
        chart = chart + " xmlns=\"http://www.w3.org/2000/svg\">"        
        
        points = count(dataSet["data"])

        // determine scale margin

        maxScaleLen = 9
        scaleMargin = maxScaleLen * 9

        if position = "right" {
            barsOffset = 0
            yAxisOffset = width - scaleMargin + 5
            scaleOffset = yAxisOffset + 5    
        } else {
            barsOffset = scaleMargin            
            yAxisOffset = barsOffset - 5
            scaleOffset = 0
        }

        // determine maximum height

        max = dataSet["data"][0]
        i = 1
        while i < points {
            if dataSet["data"][i] > max {
               max = dataSet["data"][i]
            }
            i = i + 1
        }

        // y-axis

        attr = ParseJSON("{}")
        attr["x1"] = yAxisOffset
        attr["y1"] = 0
        attr["x2"] = yAxisOffset
        attr["y2"] = height
        attr["stroke"] = "#000000"
        attr["stroke-width"] = 1
        chart = chart + rafBuildElement("line", attr, "")

        // scale values

        attr = ParseJSON("{}")
        attr["x"] = scaleOffset
        attr["y"] = 100
        attr["fill"] = "#000000"    
        chart = chart + rafBuildElement("text", attr, "Test12345")

        // configure styling attributes

        attr = ParseJSON("{}")
        attr["width"] = (width - ((points - 1) * 5) - scaleMargin) \ points
        attr["fill"] = "#00cc00"
        attr["fill-opacity"] = 0.75
        attr["stroke"] = "#004000"
        attr["stroke-opacity"] = 0.75
        attr["stroke-width"] = 1

        // create bars

        i = 0
        while i < points {
            attr["x"] = barsOffset + attr["width"] * i + (5 * i)
            attr["height"] = Round( height * (dataSet["data"][i] / max), 0 )
            attr["y"] = height - attr["height"]
            chart = chart + rafBuildElement("rect", attr, "")
            i = i + 1
        }

        chart = chart + "</svg>"

        return chart
    }

    // ***** rafBuildElement ********************

    method string rafBuildElement(string name, json attributes, string content) {
        string element
        int32 i

        element = "<" + name
        i = 0
        while i < Count(attributes) {
            element = element + " " + attributes.KeyName(i) + "=\"" + attributes[attributes.KeyName(i)] + "\""
            i = i + 1
        }
        if content <> "" {
            element = element + ">" + content + "</" + name + ">"
        } else {
            element = element + " />"
        }
       

        return element
    }

}