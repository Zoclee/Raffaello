module Rafaello {
    
    // ***** BarChart ********************

    method string BarChart(int32 width, int32 height, json dataset, json options) {
        string component
        json attr
        int32 points
        int32 i 
        int32 max

        points = count(dataset["data"])

        // determine maximum height

        max = dataset["data"][0]
        i = 1
        while i < points {
            if dataset["data"][i] > max {
               max = dataset["data"][i]
            }
            i = i + 1
        }

        // configure styling attributes

        attr = ParseJSON("{}")
        attr["width"] = (width - (points - 1) * 5) \ points
        attr["fill"] = "#00cc00"
        attr["fill-opacity"] = 0.75
        attr["stroke"] = "#004000"
        attr["stroke-opacity"] = 0.75
        attr["stroke-width"] = 1

        // create bars

        i = 0
        while i < points {
            attr["x"] =  attr["width"] * i + (5 * i)
            attr["height"] = Round( height * (dataset["data"][i] / max), 0 )
            attr["y"] = height - attr["height"]
            component = component + BuildElement("rect", attr, "")
            i = i + 1
        }

        return component
    }

    // ***** Scale ********************

    method string Scale(int32 width, int32 height, json dataset, json options) {
        string component
        json lineAttr
        json textAttr
        int32 scaleItems[]
        int32 i 
        int32 min
        int32 max
        int32 points
        int32 y
        int32 step
        string tmpStr
        int32 scaleMargin 

        points = count(dataset["data"])

        // determine min and max

        min = dataset["data"][0]
        max = dataset["data"][0]
        i = 1
        while i < points {
            if dataset["data"][i] < min {
               min = dataset["data"][i]
            }
            if dataset["data"][i] > max {
               max = dataset["data"][i]
            }
            i = i + 1
        }

        // determine scale items

        step = (max - min) \ 10
        scaleItems.Append(max)
        i = max - step
        while i >= min {
            scaleItems.Append(i)
            i = i - step
        }

        tmpStr = Str(max)
        scaleMargin = Count(tmpStr) * 7

        // draw vertical line

        lineAttr = ParseJSON("{}")
        lineAttr["x1"] = scaleMargin + 10.5 
        lineAttr["y1"] = 0
        lineAttr["x2"] = scaleMargin + 10.5
        lineAttr["y2"] = height + 1
        lineAttr["stroke"] = "#000000"
        lineAttr["stroke-width"] = 1
        component = component + BuildElement("line", lineAttr, "")

        // draw scale items

        textAttr = ParseJSON("{}")
        textAttr["fill"] = "#000000"  
        textAttr["x"] = 0
        i = 0
        while i < Count(scaleItems) {
            y = Round(height - ((scaleItems[i] - min) / (max - min)) * height, 0)
            lineAttr["x1"] = scaleMargin 
            lineAttr["y1"] = y + 0.5
            lineAttr["x2"] = scaleMargin + 10
            lineAttr["y2"] = y + 0.5
            component = component + BuildElement("line", lineAttr, "")
        
            textAttr["y"] = y
            component = component + BuildElement("text", textAttr, Str(scaleItems[i]))

            i = i + 1
        }

        return component
    }

    // ===========================================================================
    //   HELPER METHODS
    // ===========================================================================

    // ***** BuildElement ********************

    method string BuildElement(string name, json attributes, string content) {
        string element
        int32 i

        element = "<" + name
        i = 0
        while i < Count(attributes) {
            element = element + " " + attributes.KeyName(i) + "=\"" + attributes[attributes.KeyName(i)] + "\""
            i = i + 1
        }
        if content <> "" {
            element = element + ">" + content + "</" + name + ">"
        } else {
            element = element + " />"
        }
       

        return element
    }

    // ***** Render ********************

    method string Render(int32 width, int32 height, json object) {
        string composition
        json dataset
        json options
        int32 i
        int32 componentCount
        json component
        int32 datasetIndex

        composition = "<svg "
        composition = composition + " width=\"" + width + "\""
        composition = composition + " height=\"" + height + "\""
        composition = composition + " xmlns=\"http://www.w3.org/2000/svg\">"

        componentCount = Count(object["components"])
        i = 0
        while i < componentCount {

            component = object["components"][i]

            // get dataset

            if component.HasKey("dataset") {
                datasetIndex = component["dataset"]
            } else {
                datasetIndex = 0   
            } 
            dataset = object["datasets"][datasetIndex]

            options = ParseJSON("{}")

            // render component with selected dataset

            switch component["type"] {
                case "barchart" {                                       
                    composition = composition + BarChart(width, height - 1, dataset, options)
                }
                case "scale" {
                    composition = composition + Scale(width, height - 1, dataset, options)
                }
            }

            i = i + 1
        }

        composition = composition + "</svg>"

        return composition 
    }

}